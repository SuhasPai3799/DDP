/*@
import de.dfki.chatcat.util.Date;

 @*/

ask_for_name:
if (user && !user.name && !(myLastDA() >= #WHQuestion(Name))) {
  propose("ask_name") {
    emitDA(#WHQuestion(Name));
  }
  lastDAprocessed();
}

greet_back:
if (lastDA() >= #InitialGreeting(Greet)) {
  propose("greet back") {
    if (!saidInSession(#Salutation(top)))
      emitDA(#ReturnGreeting(Greet));
  }
  lastDAprocessed();
  user = new Human;
}

remember_name:
if (lastDA() >= #Inform(Name)) {
  user.name = lastDA().what;
  lastDAprocessed();
  propose("remember"){
    emitDA(#ReturnGreeting(Greet, name={user.name}));
  }
  propose("firsttime"){
    emitDA(#ReturnGreeting(Greet));
  }
}

proactive_greet:
/** use this timeout exactly once */
if (! hasActiveTimeout("robot_starts") && ! isTimedOut("robot_starts")) {
  timeout("robot_starts", 4000) {
  start_conversation:
    // Salutation is the superclass of Initial-/ReturnGreeting
    if (! (receivedInSession(#Salutation(top))
           || saidInSession(#Salutation(top)))) {
      propose("robot_greets") {
        tod = Date.timeOfDay();
        emitDA(#InitialGreeting(Greet, when={tod}));
      }
    }
  }
}

answer_courses:
/** Provides description about the course */
if (lastDA()>= #Request(Courses) && lastDA().theme=="CourseInfo")
{	
	course_name = lastDA().what;
	course_info = (String)hfcutils.answerCourseInfo(course_name);
	propose("return_course_info"){
		emitDA(#Request(Courses, cname={course_name}, info={course_info}));
	}
	lastDAprocessed();
}

answer_course_instructor:
/** Provides info about which prof is teaching the course **/
if (lastDA()>= #Request(Courses) && lastDA().theme=="CourseTeacherInfo")
{
  course_name = lastDA().what;
  course_teacher_info = (String)hfcutils.answerCourseTeacherInfo(course_name);
  propose("return_course_teacher_info")
  {
    emitDA(#Request(Courses, cname={course_name}, course_teacher_info={course_teacher_info}));
  }
  lastDAprocessed();
}

answer_dept_num_courses:
/** Provides total number of courses for the department */
if (lastDA()>= #Request(Department) && lastDA().theme=="CourseCountDept")
{
  dept_name = lastDA().what;
	tot_c = (String)hfcutils.answerDeptCourseCount(dept_name);
	propose("return_course_total")
	{
		emitDA(#Request(Department,tot_courses={tot_c}, dept_name={dept_name}));
	}
	lastDAprocessed();
}


answer_prof_courses:
/** Provides answer on courses a professor takes */
if (lastDA()>= #Request(Professor) && lastDA().theme=="CourseInfo")
{
  prof_name = lastDA().what;
  prof_courses = (String)hfcutils.answerProfCourses(prof_name);
  propose("return_prof_courses")
  {
    emitDA(#Request(Professor, prof_name={prof_name}, prof_courses = {prof_courses}));
  }
  lastDAprocessed();
}


answer_prof_research:
/** Provides answer on research areas of a prof */
if (lastDA()>= #Request(Professor) && lastDA().theme=="ResearchInfo")
{
  prof_name = lastDA().what;
  prof_areas = (String)hfcutils.answerProfResearchArea(prof_name);
  propose("return_prof_research_area")
  {
    emitDA(#Request(Professor, prof_name={prof_name}, prof_areas = {prof_areas}));
  }
  lastDAprocessed();
}