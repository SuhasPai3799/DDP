#ABNF 1.0 UTF-8;

language en-EN;
root $top;
mode voice;
tag-format "semantics/1.0";

meta "author" is "Suhas Pai";

public $top = ($greet |
              $tellName |
              $courseInfo |
              $countDeptCourses | 
              $courseTeacherInfo |
              $teacherCourseInfo |
              $teacherResearchInfo
              )  {!{ out = $%1; }!} [$punct] ;

$punct = ("$$[.;?!]"|$NULL);

$prof = (prof | professor | prof. | instructor | teacher) ;

$greet = (hi | hello) {!{ out.da="InitialGreeting" ; out.prop="Greet";}!} ;

$course = (course | courses | topics | subjects | topic | subject | topic);

$tellName = my name is "$$.*" {out.da="Inform" ; out.prop="Name" ; out.what="$$1" ;};

$teach = (teach | take | instruct | teaches | instructs | takes) ; 

$researchField = (research [field | area | topic | interest | fields | areas | topics | interests] )
                | (( area | topic | field | areas | topics | fields )  [of interest | specialization | speciality | specialities | of interests])
                | (interest | interests);

// Course related grammar rules
$courseInfo = ((tell [me] about | describe | explain | help on) [the]  
                (course "$$.*" {out.da="Request" ; out.prop="Courses" ; out.what="$$1"; out.theme="CourseInfo"} |
                "$$.*"<0-10> course {out.da="Request" ; out.prop="Courses" ; out.what="$$2"; out.theme="CourseInfo"} )) 
                
            | (what is [a|the] course "$$.*"<0-10> about {out.da="Request" ; out.prop="Courses" ; out.what="$$2"; out.theme="CourseInfo"})
            | (what is "$$.*"<0-10> course [about] {out.da="Request" ; out.prop="Courses" ; out.what="$$3"; out.theme="CourseInfo"})
            | (what is [a|the] course "$$.*"<0-10> {out.da="Request" ; out.prop="Courses" ; out.what="$$1"; out.theme="CourseInfo"});

$courseTeacherInfo = who $teach [course] "$$.*"<0-10> {out.da="Request" ; out.prop="Courses" ; out.what="$$1"; out.theme="CourseTeacherInfo";} |
                     who $teach  "$$.*"<0-10>   course {out.da="Request" ; out.prop="Courses" ; out.what="$$2"; out.theme="CourseTeacherInfo";} |
                     which $prof $teach  [course] "$$.*"<0-10> {out.da="Request" ; out.prop="Courses" ; out.what="$$1"; out.theme="CourseTeacherInfo";} |
                     which $prof $teach "$$.*"<0-10> course   {out.da="Request" ; out.prop="Courses" ; out.what="$$2"; out.theme="CourseTeacherInfo";};


$countDeptCourses = [ (could you) | please ] [ (tell | tell me ) ] how many courses does "$$.*" offer {out.da="Request"; out.prop="Department"; out.what="$$2";};

// Prof related grammar rules

$teacherCourseInfo = ( [what|which] [$course] does [$prof] "$$.*"<0-3> $teach ) {out.da="Request" ; out.prop="Professor"; out.what="$$2"; out.theme="CourseInfo";}
                     | ([what|which] [$course] [ is | are ] [taught | instructed] by [$prof] "$$.*"<0-3> ){out.da="Request" ; out.prop="Professor"; out.what="$$1"; out.theme="CourseInfo";}
                     | (what are [$prof] "$$.*"<0-4> $course )  {out.da="Request" ; out.prop="Professor"; out.what="$$2"; out.theme="CourseInfo";};

$teacherResearchInfo = ( What does [$prof] "$$.*"<0-4> (specialize|research) in ) {out.da="Request" ; out.prop="Professor"; out.what="$$3"; out.theme="ResearchInfo";}
                      |( What [is | are] [$prof] "$$.*"<0-4>  $researchField) {out.da="Request" ; out.prop="Professor"; out.what="$$2"; out.theme="ResearchInfo";};