#ABNF 1.0 UTF-8;

language en-EN;
root $top;
mode voice;
tag-format "semantics/1.0";

meta "author" is "Suhas Pai";

public $top =  ($greet |
              $tellName |
              $courseInfo |
              $countDeptCourses | 
              $courseTeacherInfo |
              $teacherCourseInfo |
              $teacherResearchInfo |
              $profResearchField |
              $deptFacilities |
              $listDeptCourses |
              $listDeptProfs |
              $listDeptUGPrograms |
              $listDeptPrograms |
              $listDeptPGPrograms |
              $deptInfo
              )  {!{ out = $%1; }!} [$punct] ;

$thanks = ("$$.*"<0->);

$punct = ("$$[.;?!]"|$NULL);

$undergrads = (undergraduates | undergrads | school students | 12th pass | UG | undergraduate | under-graduate | undergrad);

$postgrads = (postgrads | postgraduate | post-graduate | postgraduate | PG | postgrad);

$prof = (prof | professor | prof. | instructor | teacher | profs | professors | instructors | teachers) ;

$greet = (hi | hello) {!{ out.da="InitialGreeting" ; out.prop="Greet";}!} ;

$course = (course | courses | topics | subjects | topic | subject | topic);

$tellName = my name is "$$.*" {out.da="Inform" ; out.prop="Name" ; out.what="$$1" ;};

$department = (dept. | department | branch | branches | departments | dept);

$teach = (teach | take | instruct | teaches | instructs | takes) ; 

$ask = (tell | list | enumerate | explain | describe ) [me] [about] ;

$offer = (offer | offers | take | takes | give | gives | have | haves | list | lists | offered );

$labs = (labs | laboratories | lab | laboratory | workshop);

$program = (programs | program | degrees | academic programs | 4-year [program|programs] | 5-year [program|programs]);

$facilities = ([$labs] facilities | [$labs] facility | labs | lab | laboratory | laboratories | workshop | workstation | space | spaces | provisions | provision |
              equipments | equipment | amenity | amenities);

$polite = ( [ could | can ] [you] [please] [tell | describe | explain | list | enumerate | talk ] [to] [me] [about] );

$performingResearch = (performing research [in|on] | studying [in|on] | researching [in|on] | doing research [in|on] | specializing [in|on] | working [in|on]);

$researchField = (research [field | area | topic | interest | fields | areas | topics | interests] )
                | (( area | topic | field | areas | topics | fields )  [of interest | specialization | speciality | specialities | of interests])
                | (interest | interests);

// Course related grammar rules
$courseInfo = ([$polite] (tell [me] about | describe | explain | help on) [the]  
                (course "$$.*" {out.da="Request" ; out.prop="Courses" ; out.what="$$1"; out.theme="CourseInfo"} |
                "$$.*"<0-10> course {out.da="Request" ; out.prop="Courses" ; out.what="$$2"; out.theme="CourseInfo"} )) 
                
            | ([$polite] what is [a|the] course "$$.*"<0-10> about {out.da="Request" ; out.prop="Courses" ; out.what="$$2"; out.theme="CourseInfo"})
            | ([$polite] what is "$$.*"<0-10> course [about] {out.da="Request" ; out.prop="Courses" ; out.what="$$3"; out.theme="CourseInfo"})
            | ([$polite] what is [a|the] course "$$.*"<0-10> {out.da="Request" ; out.prop="Courses" ; out.what="$$1"; out.theme="CourseInfo"});

$courseTeacherInfo = [$polite] who $teach [course] "$$.*"<0-10> {out.da="Request" ; out.prop="Courses" ; out.what="$$1"; out.theme="CourseTeacherInfo";} |
                     [$polite] who $teach  "$$.*"<0-10>   course {out.da="Request" ; out.prop="Courses" ; out.what="$$2"; out.theme="CourseTeacherInfo";} |
                     [$polite] which $prof $teach  [course] "$$.*"<0-10> {out.da="Request" ; out.prop="Courses" ; out.what="$$1"; out.theme="CourseTeacherInfo";} |
                     [$polite] which $prof $teach "$$.*"<0-10> course   {out.da="Request" ; out.prop="Courses" ; out.what="$$2"; out.theme="CourseTeacherInfo";};




// Prof related grammar rules

$teacherCourseInfo = ( [$polite] [what|which] [$course] does [$prof] "$$.*"<0-3> $teach ) {out.da="Request" ; out.prop="Professor"; out.what="$$2"; out.theme="CourseInfo";}
                     | ([$polite] [what|which] [$course] [ is | are ] [taught | instructed] by [$prof] "$$.*"<0-3> ){out.da="Request" ; out.prop="Professor"; out.what="$$1"; out.theme="CourseInfo";}
                     | ([$polite] what are [$prof] "$$.*"<0-4> $course )  {out.da="Request" ; out.prop="Professor"; out.what="$$2"; out.theme="CourseInfo";};

$teacherResearchInfo = ( [$polite] What does [$prof] "$$.*"<0-4> (specialize|research) in ) {out.da="Request" ; out.prop="Professor"; out.what="$$3"; out.theme="ResearchInfo";}
                      |( [$polite] What [is | are] [$prof] "$$.*"<0-4>  $researchField) {out.da="Request" ; out.prop="Professor"; out.what="$$2"; out.theme="ResearchInfo";};



$profResearchField = ( [$polite] $prof in "$$.*"<0-4> $researchField) {out.da="Request"; out.prop="ResearchField"; out.theme="Professor"; out.what="$$2";}
                    |([$polite] [what|which] $prof (work | research | study ) [in] "$$.*"<0-4> ){out.da="Request"; out.prop="ResearchField"; out.theme="Professor"; out.what="$$1";}
                    |( [$polite] (list|enumerate|mention|tell) [all] [the] $prof $performingResearch "$$.*"<0-4>){out.da="Request"; out.prop="ResearchField"; out.theme="Professor"; out.what="$$1";}
                    |([$polite] [list|enumerate|mention|tell] (what [are]|which [are]|who [are]) [all] [the] $prof $performingResearch "$$.*"<0-4> ){out.da="Request"; out.prop="ResearchField"; out.theme="Professor"; out.what="$$1";} ;




// Department related grammar rules

$countDeptCourses = [$polite] how many courses does "$$.*"<0-4> offer {out.da="Request"; out.prop="Department"; out.what="$$2";out.theme="CourseCount";};

$deptFacilities = [$polite] (what|which) $facilities are in [the] "$$.*"<0-4> $department {out.da="Request"; out.prop="Department"; out.what="$$2"; out.theme="Facilities";}
                | [$polite] $facilities (in | on | offered by) [the] "$$.*"<0-4> $department {out.da="Request"; out.prop="Department"; out.what="$$2"; out.theme="Facilities";}
                | [$polite] "$$.*"<0-4> $department $facilities {out.da="Request"; out.prop="Department"; out.what="$$3"; out.theme="Facilities";};

$deptInfo = ($polite) "$$.*"<0-4> $department {out.da="Request";out.prop="Department"; out.what="$$2";out.theme="DeptInfo";}
        |    [$polite] $department "$$.*"<0-4> {out.da="Request";out.prop="Department"; out.what="$$1";out.theme="DeptInfo";} ;


$listDeptCourses = [$polite] what $course does "$$.*"<0-4> $department $offer [this semester | this year | this time] {out.da = "Request";out.prop="Department";out.what="$$4";out.theme="CourseList";}
                |  [$polite] what $course does $department "$$.*"<0-4>  $offer [this semester | this year | this time] {out.da = "Request";out.prop="Department";out.what="$$3";out.theme="CourseList";}
                |  [$polite] [what] $course [is | are] [offered | taught] [in | by | from] "$$.*"<0-4> $department [this semester | this year | this time]  {out.da = "Request";out.prop="Department";out.what="$$3";out.theme="CourseList";}
                |  [$polite] "$$.*"<0-4> $department $course {out.da = "Request";out.prop="Department";out.what="$$3";out.theme="CourseList";};
                
$listDeptProfs = [$polite] [what|which] $prof [$teach] [are|is] in "$$.*"<0-4> $department {out.da = "Request";out.prop="Department";out.what="$$2";out.theme="ProfList";}
                |[$polite] $prof in "$$.*"<0-4> $department {out.da = "Request";out.prop="Department";out.what="$$2";out.theme="ProfList";}
                |[$polite] "$$.*"<0-4> $department $prof {out.da = "Request";out.prop="Department";out.what="$$3";out.theme="ProfList";};

$listDeptUGPrograms = [$polite] what $undergrads $program does "$$.*"<0-4> $department $offer {out.da = "Request";out.prop="Department";out.what="$$3";out.theme="UGProgList";}
                 |  [$polite] what $undergrads $program does $department "$$.*"<0-4>  $offer {out.da = "Request";out.prop="Department";out.what="$$2";out.theme="UGProgList";}
                |  [$polite] [what] $undergrads $program [is | are] [offered | taught] [in | by | from] "$$.*"<0-4> $department {out.da = "Request";out.prop="Department";out.what="$$2";out.theme="UGProgList";}
                |  [$polite] "$$.*"<0-4> $department $undergrads  $program {out.da = "Request";out.prop="Department";out.what="$$4";out.theme="UGProgList";};

$listDeptPGPrograms = [$polite] what $postgrads $program does "$$.*"<0-4> $department $offer {out.da = "Request";out.prop="Department";out.what="$$3";out.theme="PGProgList";}
                 |  [$polite] what $postgrads $program does $department "$$.*"<0-4>  $offer {out.da = "Request";out.prop="Department";out.what="$$2";out.theme="PGProgList";}
                |  [$polite] [what] $postgrads $program [is | are] [offered | taught] [in | by | from] "$$.*"<0-4> $department {out.da = "Request";out.prop="Department";out.what="$$2";out.theme="PGProgList";}
                |  [$polite] "$$.*"<0-4> $department $postgrads $program {out.da = "Request";out.prop="Department";out.what="$$4";out.theme="PGProgList";};

$listDeptPrograms = [$polite] what $program does "$$.*"<0-4> $department $offer {out.da = "Request";out.prop="Department";out.what="$$3";out.theme="ProgList";}
                 |  [$polite] what $program does $department "$$.*"<0-4>  $offer {out.da = "Request";out.prop="Department";out.what="$$2";out.theme="ProgList";}
                |  [$polite] [what] $program [is | are] [offered | taught] [in | by | from] "$$.*"<0-4> $department {out.da = "Request";out.prop="Department";out.what="$$2";out.theme="ProgList";}
                |  [$polite] "$$.*"<0-4> $department $program {out.da = "Request";out.prop="Department";out.what="$$3";out.theme="ProgList";}
                |  (what|which) [are|is] $program in "$$.*"<0-4> $department {out.da = "Request";out.prop="Department";out.what="$$2";out.theme="ProgList";};


              